FINISH  
/CLEAR,START
/PREP7  
! No polynomial elements
/PMETH,OFF,1

! Set electric preferences
KEYW,PR_ELMAG,1
KEYW,MAGELC,1   

! Select element
ET,1,SOLID123

! Material properties
MP,PERX,1,4        ! Permittivity of DLC = XC Kapton Dupont
MP,RSVX,1,5.e6     ! Resistivity of DLC = XC Kapton Dupont
MP,PERX,2,4.0      ! Permittivity of FR4
MP,PERX,3,1.0      ! Permittivity of Gas


! Construct the FTM ... all dimensions in mm
pitch   = 0.500       ! Distance between holes      (500um)
dlc     = 0.025       ! Thickness of XC layers       (25um)
kapton1 = 0.200-dlc   ! Thickness of PCB with holes (200um)    --> change from 0.200 to 0.175 to embed 0.025 XC kapton
kapton2 = 0.200       ! Thickness of PCB support    (200um)
outdia  = 0.200       ! Hole outer diameter         (200um)
india   = 0.200       ! Hole inner diameter         (200um)    ... FTM V3 has cyl holes
rim     = 0.220       ! Rim diameter                (220um)    ... not used for FTM V3
driVol = 2            ! Total Drift Volume         (2000um)    ... to be changed when changing nlayer
nlayer = 2            ! Number of layers                       ... FTM V3 had 4 layers
drift = driVol/nlayer                      !   (1000um)    ... FTM V3 has 1mm for each drift region
thick = drift+dlc+kapton2+kapton1          !   (1400um)    ... total thickness of single layer             


! lets start at z=0 and start building up ...
*do,il,1,nlayer,1
coff = (il-1)*thick
BLOCK, 0, pitch/2, 0, sqrt(3)*pitch/2,                           0+coff,                     kapton2+coff  ! support pcb (kapton2)     ---> volumes 1+4*(il-1)
BLOCK, 0, pitch/2, 0, sqrt(3)*pitch/2,                     kapton2+coff,             kapton1+kapton2+coff  ! pcb with holes (kapton1)  ---> volumes 2+4*(il-1)
BLOCK, 0, pitch/2, 0, sqrt(3)*pitch/2,             kapton1+kapton2+coff,       kapton1+1*dlc+kapton2+coff  ! amp (dlc)                 ---> volumes 3+4*(il-1)
BLOCK, 0, pitch/2, 0, sqrt(3)*pitch/2,                    +kapton2+coff, drift+kapton1+1*dlc+kapton2+coff  ! drift (gas)               ---> volumes 4+4*(il-1)  !!! gas must overlap with PCB(2) and DLC(3)
*enddo
 
! first hole for each of the layers
newvol = 4*nlayer+1                   ! each FTM layer consists of 4 layers (see above), next free volume is 4*nlayer+1
*do, il,1,nlayer,1                    ! loop again over all layers
coff = (il-1)*thick
WPOFFS, 0, 0, coff                    ! move to the correct height
! Make the pieces to substract from pcb and dlc to create the holes
! conical holes
! CONE, outdia/2, middia/2,               kapton2+coff,       kapton1+kapton2+coff, 0, 360 ! Makes volume newvol
! CONE, outdia/2, middia/2,       kapton1+kapton2+coff, kapton1+1*dlc+kapton2+coff, 0, 360 ! Makes volume newvol+1
! cylindrical holes: CYCL4 command works with depth, not with zmin,zmax ... therefore we have to use the offset command first
WPOFFS, 0, 0, kapton2                 ! go to bottom of PCB with holes (and ready to drill in + direction)
CYL4, 0, 0, outdia/2,,,, kapton1      ! Makes volume newvol
WPOFFS, 0, 0, kapton1                 ! go to top of PCB with holes (and ready to drill in + direction)
CYL4, 0, 0, outdia/2,,,, dlc          ! Makes volume newvol+1
WPOFFS, 0, 0, -(kapton2+kapton1)      ! Move back
VADD, newvol, newvol+1                ! Makes volume newvol+1+il and frees newvol and newvol+1
WPOFFS, 0, 0, -coff                   ! move back to original height
*enddo

! second hole for each of the layers
*do, il,1,nlayer,1
coff = (il-1)*thick
WPOFFS, pitch/2, sqrt(3)*pitch/2, 0   ! move to the opposite angle of the block
WPOFFS, 0, 0, coff                    ! move to the right height
! Make the pieces to substract from pcb and dlc to create the holes
! conical holes
! CONE, outdia/2, middia/2,               kapton2+coff,       kapton1+kapton2+coff, 0, 360 ! Makes volume newvol
! CONE, outdia/2, middia/2,       kapton1+kapton2+coff, kapton1+1*dlc+kapton2+coff, 0, 360 ! Makes volume newvol+1
! cylindrical holes: CYCL4 command works with depth, not with zmin,zmax ... therefore we have to use the offset command first
WPOFFS, 0, 0, kapton2    	      ! go to bottom of PCB with holes (and ready to drill in +	direction)
CYL4, 0, 0, outdia/2,,,, kapton1      ! Makes volume newvol
WPOFFS, 0, 0, kapton1                 ! go to top of PCB with holes (and ready to drill in	+ direction)
CYL4, 0, 0, outdia/2,,,, dlc          ! Makes volume newvol+1
WPOFFS,	 0, 0, -(kapton2+kapton1)     ! Move back
VADD, newvol, newvol+1                ! Makes volume newvol+1+nlayer+il and frees newvol and newvol+1
WPOFFS, 0, 0, -coff                   ! move back to original height
WPOFFS, -pitch/2, -sqrt(3)*pitch/2, 0 ! move back to initial angle (0,0,0)
*enddo

! summary of the situation
! volumes 1 --> 4*nlayer are the rectangular volumes (pcb/pcb/dlc/gas) for each of the layers
! 4*nlayer+1 and 4*nlayer+2 were used in intermediate steps and are at this point free

*do, il,1,nlayer,1
kp=2+4*(il-1)  ! counter for PCB with holes (see above)
dl=3+4*(il-1)  ! counter for DLC with holes (see above)
! Subtract the two holes from the standard kapton / PCB
VSBV,  kp, newvol+1+il, , , KEEP              !  first hole substracted: kp -> newvol
VSBV,  newvol, newvol+1+nlayer+il, , , KEEP   ! second hole substracted: newvol -> kp
! Subtract the two holes from the dlc
VSBV,  dl, newvol+1+il, , , KEEP              !  first hole substracted: dl -> newvol
VSBV,  newvol, newvol+1+nlayer+il, , , KEEP   ! second hole substracted: newvol -> dl
*enddo

! Delete the holes
*do, il,1,nlayer,1
VDEL, newvol+1+il,newvol+1+nlayer+il
*enddo

! Subtract the standard kapton and dlc from the gas
*do, il,1,nlayer,1
kp = 2+4*(il-1) ! counter for PCB with holes (see above)
dl = 3+4*(il-1) ! counter for DLC with holes (see above)
ga = 4+4*(il-1) ! counter for GAS (see above)
VSBV,  ga, kp, , , KEEP       ! gas -> newvol
VSBV,  newvol, dl, , , KEEP   ! newcol -> gas
*enddo

! if you want to color at this point you can use these commands
! *do, il,1,nlayer,1
! ks = 1+4*(il-1) ! counter for support PCB
! kp = 2+4*(il-1) ! counter for PCB with holes
! dl = 3+4*(il-1) ! counter for DLC (amp)
! ga = 4+4*(il-1) ! counter for GAS (see above)
! /COLOR, VOLU, ORANGE, ks
! /COLOR, VOLU, BLUE, kp
! /COLOR, VOLU, RED, dl
! /COLOR, VOLU, GREEN, ga
! *enddo

! Glue everything together
! NOORDER    ! reduce confusion after gluing ... keep same order for numbering ... not computationally efficient
VGLUE, ALL
NUMCMP,ALL   ! fills all gaps .. creates also a mess in the volume ordering

! Glueing however seems to randomnize the volume numbering :-(
! Output after VGLUE, ALL (for 2 Layers) but before NUMCMP,ALL:
! GLUE VOLUMES
!       INPUT VOLUMES =     1    2    3    4    5    6    7    8    9   10
!       INPUT VOLUMES =    11   12
!       INPUT VOLUMES WILL BE DELETED IF POSSIBLE
!       INPUT VOLUMES =     1    2    3    4    5    6
!       INPUT VOLUMES WILL BE DELETED IF POSSIBLE
!       OUTPUT VOLUMES =    1    7    8    9   10    11
! Output after NUMCMP,ALL
!       OUTPUT VOLUMES =     1    2    3    4    5    6

/COLOR, VOLU, ORANGE, 1  ! bottom pcb support
/COLOR, VOLU, BLUE, 2    ! bottom pcb with holes
/COLOR, VOLU, RED, 3     ! bottom dlc amplification
/COLOR, VOLU, GREEN, 7   ! bottom gas

/COLOR, VOLU, ORANGE, 4  ! top pcb support
/COLOR, VOLU, BLUE, 5    ! top pcb with holes
/COLOR, VOLU, RED, 6     ! top dlc amplification
/COLOR, VOLU, GREEN, 8  ! top gas


! Assign Material attributes to the volumes
! Now it is important to know which volume is which
! As far as I know this is a trial and error process

! dlc :: material 1 --- volumes 3,6
VSEL,S,,, 3
VATT, 1,,1
VSEL,S,,, 6
VATT, 1,,1

! pcb :: material 2 --- volumes 1,2,4,5
VSEL,S,,, 1
VATT, 2, ,1
VSEL,S,,, 2
VATT, 2, ,1
VSEL,S,,, 4
VATT, 2, ,1
VSEL,S,,, 5
VATT, 2, ,1

! gas :: material 3 --- volumes 7,8
VSEL,S,,, 7
VATT, 3, ,1
VSEL,S,,, 8
VATT, 3, ,1

! Potentials calculation
! Field in kV/cm
a_field=50 ! amp field 50kV/cm
d_field=4  ! drift field 4kV/cm
! geometry is in mm --> *0.1
! conversion kV to V ---> *1000
! inverse vields ---> -1
ground = 0
amp    = -1*a_field*0.1*(kapton1+dlc)*1000+ground
drift  = -1*d_field*0.1*drift*1000+amp


! Voltage setting :: at ground :: surface at kapton2 height
ASEL, S, LOC, Z, kapton2
DA,ALL,VOLT,ground
ASEL, S, LOC, Z, thick+kapton2
DA,ALL,VOLT,ground

! Voltage setting :: at amp :: volumes 3,6
! VSEL,S,VOLU,,3
VSEL,S,,,3
ASLV,S
DA,ALL,VOLT,amp
! VSEL,S,VOLU,,6
VSEL,S,,,6
ASLV,S
DA,ALL,VOLT,amp

! Voltage setting :: at drift :: surface at thick
ASEL, S, LOC, Z, 1*thick
DA,ALL,VOLT,drift
ASEL, S, LOC, Z, 2*thick
DA,ALL,VOLT,drift


! Meshing options
! All volumes and areas are part of the model
! This should never be uncommented ... 
! But I have no clue why ....
! Without these lines the fine mesh for PI & Gas only does not work
VSEL,S,,,1,8
ASLV,S

! Mesh all volumes
! this works :: potentials are set correctly
! MSHKEY,0
! SMRT,1
! VMESH,1,8


! Mesh only gas and polyimide
MSHKEY, 0
SMRT, 1
VSEL, S,,,7 ! select bottom gas
VSEL, A,,,8 ! add top gas to selection
VSEL, A,,,1  ! add bottom pcb support
VSEL, A,,,2  ! add bottom pcb amp
VSEL, A,,,4  ! add top pcb support
VSEL, A,,,5  ! add top pcb amp
! VSEL, A,,,3  ! add bottom dlc support      ! -- should not be added
! VSEL, A,,,6  ! add top dlc amp             ! -- should not be added
VMESH, ALL



! Solve the field
/SOLU
SOLVE
FINISH

! Display the solution
/POST1
/EFACET,1
PLNSOL,VOLT,,0

! Write the solution to a file
/OUTPUT, PRNSOL, lis  !	file with name PRNSOL.lis
PRNSOL
/OUTPUT

! Write the list of nodes to a file
/OUTPUT, NLIST, lis   !	file with name NLIST.lis
NLIST,,,,COORD
/OUTPUT

! Write the list of elements to a file
/OUTPUT, ELIST, lis   ! file with name ELIST.lis
ELIST
/OUTPUT

! Write the material properties to a file
/OUTPUT, MPLIST, lis  ! file with name MPLIST.lis
MPLIST
/OUTPUt
